/*
 * Copyright 2017 TomTom International B.V.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'
apply plugin: 'groovy'

sourceCompatibility = 1.8

jar {
    manifest {
        attributes 'Agent-Class': 'com.tomtom.james.newagent.JVMAgent',
                'Premain-Class': 'com.tomtom.james.newagent.JVMAgent',
                'Can-Redefine-Classes': true,
                'Can-Retransform-Classes': true,
                'Multi-Release': true
    }
}

shadowJar {
    exclude '**/services/org.apache.logging.log4j.util.*'
    exclude('module-info.class', '**/9/module-info.class')
    relocate 'com.google', 'com.tomtom.james.repackaged.com.google'
    relocate 'com.fasterxml.jackson', 'com.tomtom.james.repackaged.com.fasterxml.jackson'
    relocate 'org.yaml.snakeyaml', 'com.tomtom.james.repackaged.org.yaml.snakeyaml'
    relocate 'com.lmax.disruptor', 'com.tomtom.james.repackaged.com.lmax.disruptor'
    relocate 'org.apache.logging', 'com.tomtom.james.repackaged.org.apache.logging'
    relocate 'META-INF.versions.9.org.apache.logging', 'META-INF.versions.9.com.tomtom.james.repackaged.org.apache.logging'
    relocate 'co.elastic.logging', 'com.tomtom.james.repackaged.co.elastic.logging'
}
assemble.dependsOn(shadowJar)

dependencies {
    implementation project(':james-agent-common')
    implementation project(':james-agent-io')

    implementation(group: 'com.google.guava', name: 'guava', version: versions.guava) {
        exclude group: 'com.google.code.findbugs', module: 'jsr305'
        exclude group: 'com.google.errorprone', module: 'error_prone_annotations'
        exclude group: 'com.google.j2objc', module: 'j2objc-annotations'
        exclude group: 'org.codehaus.mojo', module: 'animal-sniffer-annotations'
        exclude group: 'org.checkerframework', module: 'checker-qual'
    }
    implementation group: 'org.yaml', name: 'snakeyaml', version: versions.snakeyaml
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jackson
    implementation group: 'org.codehaus.groovy', name: 'groovy', version: versions.groovy
    implementation group: 'org.codehaus.groovy', name: 'groovy-json', version: versions.groovy
    implementation group: 'org.codehaus.groovy', name: 'groovy-xml', version: versions.groovy
    implementation group: 'org.javassist', name: 'javassist', version: versions.javassist
    implementation group: 'com.lmax', name: 'disruptor', version: versions.disruptor
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: versions.log4j_stackutil

    testImplementation group: 'org.spockframework', name: 'spock-core', version: versions.spock
    testImplementation group: 'org.awaitility', name: 'awaitility', version: versions.awaitility
    testRuntimeOnly group: 'org.objenesis', name: 'objenesis', version: versions.objenesis
    testRuntimeOnly group: 'net.bytebuddy', name: 'byte-buddy', version: versions.bytebuddytests
}
