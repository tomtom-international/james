/*
 * Copyright 2017 TomTom International B.V.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:6.1.0"
        classpath "io.github.gradle-nexus:publish-plugin:1.1.0"
    }
}
apply plugin: "io.github.gradle-nexus.publish-plugin"

allprojects {
    if(project.hasProperty('releaseVersion')) {
        version = project.releaseVersion
    } else {
        version = '1.0-SNAPSHOT'
    }
    group = 'com.github.tomtom-international.james'
}

nexusPublishing {
    repositories {
        sonatype {
            username = project.properties["ossrhUsername"]
            password = project.properties["ossrhPassword"]
        }
    }
}

def projectDescriptions = [
        'james-agent-common': 'A package that lets you write your own toolkits for James',
        'james-controller-webservice-api': 'API for REST controller of James',
        'james-controller-webservice-client': 'Client for REST controller of James'
]

subprojects { prj ->
    repositories {
      mavenCentral()
    }
    apply plugin : 'java'
    if (!projectDescriptions.containsKey(prj.name)) {
        apply plugin: 'com.github.johnrengelman.shadow'
    }
    ext.versions = [
            groovy      : '3.0.8',
            guava       : '31.1-jre',
            jackson     : '2.13.1',
            snakeyaml   : '1.28',
            spock       : '2.0-groovy-3.0',
            objenesis   : '3.2',
            awaitility  : '4.1.0',
            javassist   : '3.28.0-GA',
            bytebuddytests: '1.11.1',
            disruptor: '3.4.2',
            log4j_stackutil: '2.17.1',
            ecs_logging: '1.1.0'
    ]
    test.useJUnitPlatform()
}

def projectList = subprojects.findAll { projectDescriptions.keySet().contains(it.name) }

configure(projectList) { Project confPrj ->
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    java {
        withJavadocJar()
        withSourcesJar()
    }
    if(project.hasProperty('releaseVersion')) {
        signing {
          sign publishing.publications
        }
    }
    publishing {
      publications {
        sonatype(MavenPublication) {
          from components.java
          pom {
            name = confPrj.name
            packaging = 'jar'
            description  = projectDescriptions[confPrj.name]
            url = 'https://github.com/tomtom-international/james'

            scm {
              connection = 'scm:git:https://github.com/tomtom-international/james.git'
              developerConnection = 'scm:git:https://github.com/tomtom-international/james.git'
              url = 'https://github.com/tomtom-international/james'
            }

            licenses {
              license {
                name = 'The Apache License, Version 2.0'
                url = 'http://www.apache.org/licenses/LICENSE-2.0'
              }
            }
            developers {
              developer {
                  id = 'james'
                  name = 'James'
                  email = 'james@groups.tomtom.com'
              }
            }
          }
        }
      }
    }
}

wrapper {
    gradleVersion = '7.0.2'
}
