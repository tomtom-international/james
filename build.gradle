/*
 * Copyright 2017 TomTom International B.V.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.1"
    }
}

allprojects {
    if(project.hasProperty('releaseVersion')) {
        version = project.releaseVersion
    } else {
        version = '1.0-SNAPSHOT'
    }
    group = 'com.github.tomtom-international.james'
}

subprojects {
    repositories {
        jcenter()
    }

    apply plugin: 'com.github.johnrengelman.shadow'

    ext.versions = [
            bytebuddy   : '1.7.8',
            groovy      : '2.4.12',
            guava       : '22.0',
            jackson     : '2.8.7',
            snakeyaml   : '1.18',
            spock       : '1.1-groovy-2.4',
            objenesis   : '2.5.1',
            awaitility  : '3.0.0',
            commonslang : '3.7',
            javassist   : '3.22.0-GA',
            suntools    : '1.5.0'
    ]
}

wrapper {
    gradleVersion = '4.2'
}

def projectDescriptions = [
  'james-agent-common': 'A package that lets you write your own toolkits for James',
  'james-controller-webservice-api': 'API for REST controller of James',
  'james-controller-webservice-client': 'Client for REST controller of James'
]

def projectList = subprojects.findAll { projectDescriptions.keySet().contains(it.name) }

configure(projectList) { confPrj ->
  apply plugin: 'java'
  apply plugin: 'maven'

  if(project.hasProperty('releaseVersion')) {
    apply plugin: 'signing'
    signing {
        sign configurations.archives
    }

    task javadocJar(type: Jar) {
      classifier = 'javadoc'
      from javadoc
    }

    task sourcesJar(type: Jar) {
      classifier = 'sources'
      from sourceSets.main.allSource
    }

    artifacts {
      archives javadocJar, sourcesJar
    }

    uploadArchives {
      repositories {
        mavenDeployer {
          beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

          repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
            authentication(userName: ossrhUsername, password: ossrhPassword)
          }

          pom.project {
            name confPrj.name
            packaging 'jar'
            description projectDescriptions[confPrj.name]
            url 'https://github.com/tomtom-international/james'

            scm {
              connection 'scm:git:https://github.com/tomtom-international/james.git'
              developerConnection 'scm:git:https://github.com/tomtom-international/james.git'
              url 'https://github.com/tomtom-international/james'
            }

            licenses {
              license {
                name 'The Apache License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0'
              }
            }

            developers {
              developer {
                id 'pdebicki'
                name 'Piotr DÄ™bicki'
                email 'piotr.debicki@tomtom.com'
              }
              developer {
                id 'alibich'
                name 'Artur Libich'
                email 'artur.libich@tomtom.com'
              }
            }
          }
        }
      }
    }
  }
}